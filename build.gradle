import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' apply false
	id 'org.springframework.experimental.aot' apply false
	id 'org.graalvm.buildtools.native' apply false
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'


subprojects {

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'org.springframework.experimental.aot'
	apply plugin: 'org.graalvm.buildtools.native'

	sourceCompatibility = '11'

	repositories {
		maven { url 'https://repo.spring.io/release' }
		mavenCentral()
	}

	dependencies {
		// avoid use of 'io.spring.dependency-management' plugin
		implementation enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
	}

	bootBuildImage {
		builder = 'paketobuildpacks/builder:tiny'
		environment = ['BP_NATIVE_IMAGE': 'true']
	}

	nativeBuild {
		classpath processAotResources.outputs, compileAotJava.outputs
	}

	nativeTest {
		classpath processAotTestResources.outputs, compileAotTestJava.outputs
	}

}
